
/* JNMontResurrected = 1 means Montaron was resurrected and is free 
JNMontResurrected = 2 means Montaron was cursed with a geas on his resurrection

JNMontbg1 = 1 means Montaron traveled with the player in BG1
JNMontbg1 = 0 means he didn't

JNMontRedemption can be increased and at a certain point allows for a different ending for Montaron
currently it can at max get 1

*/

IF
    See([PC])
    NumTimesTalkedTo(0)
THEN
    RESPONSE #100
        Dialogue([PC])
END

/* Script to set XP */

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPLT(Player1,160000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,159999)
	XPLT(Player1,250000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",35000)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,249999)
	XPLT(Player1,500000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",125000)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,499999)
	XPLT(Player1,750000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",375000)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,749999)
	XPLT(Player1,1000000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",625000)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,999999)
	XPLT(Player1,1250000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",875000)
		Continue()
END

/* ==========================================
======== Standard Talks while Joined ========
========================================== */

IF 
	InParty(Myself)
	Global("JNMONTTalk","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("JNMONTTimer","GLOBAL",3600)
		SetGlobal("JNMONTTalk","GLOBAL",1)
END

IF
	InParty(Myself)
	RealGlobalTimerExpired("JNMONTTimer","GLOBAL")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	OR(4)
	Global("JNMONTTalk","GLOBAL",1)
	Global("JNMONTTalk","GLOBAL",3)
	Global("JNMONTTalk","GLOBAL",5)
	Global("JNMONTTalk","GLOBAL",7)
THEN
	RESPONSE #100
		IncrementGlobal("JNMONTTalk","GLOBAL",1)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	OR(4)
	Global("JNMONTTalk","GLOBAL",2)
	Global("JNMONTTalk","GLOBAL",4)
	Global("JNMONTTalk","GLOBAL",6)
	Global("JNMONTTalk","GLOBAL",8)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* ==============================
======== Scripted Talks =========
============================== */

// Talk for Quest 1 Trigger

IF 
	InParty(Myself)
	Global("JNMontQuest1Timer","GLOBAL",0)
	Global("JNMontResurrected","GLOBAL",2)
THEN
	RESPONSE #100
		RealSetGlobalTimer("JNMontQuest1Timer","GLOBAL",600)
		SetGlobal("JNMontQuest1","GLOBAL",1)
END

IF
	InParty(Myself)
	RealGlobalTimerExpired("JNMontQuest1Timer","GLOBAL")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("JNMontQuest1","GLOBAL",1)
	Global("JNMontResurrected","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("JNMontQuest1","GLOBAL",2)
		StartDialogueNoSet(Player1)
END

// Talk at Xzar's place of death

IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("JNMONTXzarTalk","AR0300",0)
	NearLocation(Myself,2362,2489,15)
	CombatCounter(0)
	!See([ENEMY])
	AreaCheck("AR0300")		// Docks
THEN
	RESPONSE #100
		SetGlobal("JNMONTXzarTalk","AR0300",1)
		StartDialogueNoSet(Player1)
END

// Spawn Zhentarim Thief in Xzar's Hideout
// JNVESPLives = 1 means Vesper was let go by the player
// JNVESPLives = 2 means Vesper was killed by the player

IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("JNMONTVisitedXzarDeath","Global",1)
	Global("JNMONTVespSpawned","AR0407",0)
	AreaCheck("AR0407")		// Prebek's Home
THEN
	RESPONSE #100
		SetGlobal("JNMONTVespSpawned","AR0407",1)
		CreateCreature("JNVESP",[374.252],14)
		GiveItemCreate("JNDAG01","JNVESP",0,0,0)
END

// Talk after Montaron's Dagger was retrieved

IF 
	InParty(Myself)
	Global("JNMontVespTalk","GLOBAL",0)
	Dead("JNVESP")
	!Global("JNVESPLives","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("JNMontVespTalkTimer","GLOBAL",300)
		SetGlobal("JNMontVespTalk","GLOBAL",1)
END

IF
	InParty(Myself)
	RealGlobalTimerExpired("JNMontVespTalkTimer","GLOBAL")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("JNMontVespTalk","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("JNMontVespTalk","GLOBAL",2)
		StartDialogueNoSet(Player1)
END
