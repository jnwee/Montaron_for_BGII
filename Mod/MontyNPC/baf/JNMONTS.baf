
/*
JNMONTBg1 = 1 means Montaron traveled with the player in BG1
JNMONTBg1 = 0 means he didn't

JNMONTRedemption can be increased and at a certain point allows for a different ending for Montaron
currently it can at max get 1

JNMONTResurrected == 1 set in Priest Dialogue, use maybe for ToB?
*/

IF
    See([PC])
    NumTimesTalkedTo(0)
THEN
    RESPONSE #100
        Dialogue([PC])
END

/* Script to set XP */

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPLT(Player1,160000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,159999)
	XPLT(Player1,250000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",35000)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,249999)
	XPLT(Player1,500000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",125000)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,499999)
	XPLT(Player1,750000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",375000)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,749999)
	XPLT(Player1,1000000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",625000)
		Continue()
END

IF
	InParty("JNMONT")
	Global("JNMONTExperienceSet","GLOBAL",0)
	XPGT(Player1,999999)
	XPLT(Player1,1250000)
THEN
	RESPONSE #100
		SetGlobal("JNMONTExperienceSet","GLOBAL",1)
		AddXPObject("JNMONT",875000)
		Continue()
END

/* ==============================
======== Scripted Talks =========
============================== */

// Talk to urge visiting Xzar JNMONTXzarTalk < 3
// JNMONTXzarTalk == 6 -> Montaron leaves

IF
	InParty(Myself)
	Global("JNMONTXzarTalk","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("JNMONTVisitXzarTimer","GLOBAL",600)
		SetGlobal("JNMONTXzarTalk","GLOBAL",1)
END

IF 
	InParty(Myself)
	RealGlobalTimerExpired("JNMONTVisitXzarTimer","GLOBAL")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	OR(3)
	Global("JNMONTXzarTalk","GLOBAL",1)
	Global("JNMONTXzarTalk","GLOBAL",3)
	Global("JNMONTXzarTalk","GLOBAL",5)
THEN
	RESPONSE #100
		IncrementGlobal("JNMONTXzarTalk","GLOBAL",1)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	OR(3)
	Global("JNMONTXzarTalk","GLOBAL",2)
	Global("JNMONTXzarTalk","GLOBAL",4)
	Global("JNMONTXzarTalk","GLOBAL",6)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

// Talk at Xzar's place of death JNMONTXzarTalk == 8

IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	GlobalLT("JNMONTXzarTalk","GLOBAL",7)
	NearLocation(Myself,2362,2489,15)
	CombatCounter(0)
	!See([ENEMY])
	AreaCheck("AR0300")		// Docks
THEN
	RESPONSE #100
		SetGlobal("JNMONTXzarTalk","GLOBAL",8)
		StartDialogueNoSet(Player1)
END





// Spawn Zhentarim Thief in Xzar's Hideout
// JNVESPLives = 1 means Vesper was let go by the player
// JNVESPLives = 2 means Vesper was killed by the player

IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("JNMONTVisitedXzarDeath","Global",1)
	Global("JNMONTVespSpawned","AR0407",0)
	AreaCheck("AR0407")		// Prebek's Home
THEN
	RESPONSE #100
		SetGlobal("JNMONTVespSpawned","AR0407",1)
		CreateCreature("JNVESP",[374.252],14)
		GiveItemCreate("JNDAG01","JNVESP",0,0,0)
END

// Talk after Vesper is dead
IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!Global("JNVESPLives","GLOBAL",0)
	Global("JNMONTVespTalk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("JNMONTVespTalk","GLOBAL",1)
		StartDialogueNoSet(Player1)
END
	
// Chapter 5 Talk

IF
    InParty(Myself)
    Global("Chapter","GLOBAL",%bg2_chapter_5%)
    Global("JNMONTChapter5","GLOBAL",0)
THEN
    RESPONSE #100
        SetGlobal("JNMONTChapter5","GLOBAL",1)
        RealSetGlobalTimer("JNMONTChapter5Timer","GLOBAL",1800)
END

IF
    InParty(Myself)
    RealGlobalTimerExpired("JNMONTChapter5Timer","GLOBAL")
    See(Player1)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !See([ENEMY])
    Global("Chapter","GLOBAL",%bg2_chapter_5%)
    Global("JNMONTChapter5","GLOBAL",1)
THEN
    RESPONSE #100
        SetGlobal("JNMONTChapter5","GLOBAL",2)
        StartDialogueNoSet(Player1)
END
